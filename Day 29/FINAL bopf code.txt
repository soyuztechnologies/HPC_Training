@AbapCatalog.sqlViewName: 'ZHPCXXCDSBOPF'
@AbapCatalog.compiler.compareFilter: true
@AbapCatalog.preserveKey: true
@AccessControl.authorizationCheck: #CHECK
@EndUserText.label: 'CDS BOPF for transactional fiori app'
@VDM.viewType: #CONSUMPTION
@OData.publish: true
@Metadata.allowExtensions: true

@ObjectModel:{
    modelCategory: #BUSINESS_OBJECT,
    compositionRoot: true,
    writeActivePersistence: 'ZOFT_MEALS',
    writeDraftPersistence: 'ZOFT_MEALS_HPC',
    transactionalProcessingEnabled: true,
    createEnabled: true,
    updateEnabled: true,
    deleteEnabled: true,
    draftEnabled: true
}

define view ZHPC_XX_CDS_BOPF as select from zoft_meals as oft 
association[1] to scarr as _Airline on
oft.carrid = _Airline.carrid
association[1] to spfli as _Flights on
oft.carrid = _Flights.carrid and oft.connid = _Flights.connid
association[1] to smeal as _Meals on
oft.meal = _Meals.mealnumber and oft.carrid = _Meals.carrid
{

    key meal_id as Meal_Id,
    
    carrid as Carrid,
    
    connid as Connid,
    meal as Meal,
    @ObjectModel.readOnly: true
    total_seats as Total_Seats,
     @EndUserText.label: 'Approval Status'
    status as Status,
    _Airline,
    _Flights,
    _Meals
}



class ZCL_HPC_A_APPROVE definition
  public
  inheriting from /BOBF/CL_LIB_A_SUPERCL_SIMPLE
  final
  create public .

public section.

  methods /BOBF/IF_FRW_ACTION~EXECUTE
    redefinition .
protected section.
private section.
ENDCLASS.



CLASS ZCL_HPC_A_APPROVE IMPLEMENTATION.
  method /BOBF/IF_FRW_ACTION~EXECUTE.


       data : lt_data type ZTHPC_XX_CDS_BOPF.

       "Read data from the screen which was given to BOPF framework
       "Every transaction in BOPF has a node key

       io_read->retrieve(
         EXPORTING
           iv_node                 = is_ctx-node_key
           it_key                  = it_key
         IMPORTING
            et_data                 = lt_data
       ).

       "always one record which we get
       read table lt_data reference into data(lo_wa) index 1.

       "Select data from table to check if airline code is valid
       lo_wa->status = abap_true.

       "update bopf that value needs to be changed
       io_modify->update(
         EXPORTING
           iv_node           = is_ctx-node_key
           iv_key            = lo_wa->key
           is_data           =  lo_wa
           it_changed_fields = value #( ( ZIF_HPC_XX_CDS_BOPF_C=>sc_node_attribute-zhpc_xx_cds_bopf-status ) )
       ).


  endmethod.
ENDCLASS.


@Metadata.layer: #CORE
@UI.headerInfo:{
    typeName: 'Travel',
    typeNamePlural: 'Travels',
    title: { label: 'Carrier' , value: 'Carrid' },
    description: { value: 'Connid'}
}

annotate view ZHPC_XX_CDS_BOPF
    with 
{
     @UI.facet: [{
        type: #COLLECTION,
        id: 'collection',
        purpose: #STANDARD,
        label: 'More Info'
     },{
        type: #IDENTIFICATION_REFERENCE,
        purpose: #STANDARD,
        label: 'More Info',
        parentId: 'collection',
        position: 1
     },{
        type: #FIELDGROUP_REFERENCE,
        purpose: #STANDARD,
        label: 'Additional Fields',
        parentId: 'collection',
        id: 'fg1',
        position: 2
    }] 
    Meal_Id;
    @UI.selectionField: [{position: 1 }]
    @UI.lineItem: [{position: 1 }]
    @UI.identification: [{position: 1 }]
    Carrid;
    @UI.selectionField: [{position: 2 }]
    @UI.lineItem: [{position: 2 }]
    @UI.identification: [{position: 2 }]
    Connid;
    @UI.lineItem: [{position: 3 }]
    @UI.fieldGroup: [{position: 1, targetElement: 'fg1' }]
    
    Meal;
    @UI.fieldGroup: [{position: 2, targetElement: 'fg1' }]
    @UI.lineItem: [{position: 4 }]
    
    Total_Seats;
    @UI.fieldGroup: [{position: 3, targetElement: 'fg1' }]
    @UI.lineItem: [{position: 5, type: #FOR_ACTION, dataAction: 'BOPF:APPROVE', label: 'Approve' }]
   
    Status;
    
}


